import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

android {

    def appName = "indoor_flowers"
    def packageName = "com.indoor.flowers"
    def authority = packageName + ".database";

    def versionMajor = 0 // 1 - 20000
    def versionMinor = 0 // 0 - 99
    def versionPatch = 1 // 0 - 999

    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId packageName
        minSdkVersion 19
        targetSdkVersion 25
        versionCode versionMajor * 100000 + versionMinor * 1000 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField 'String', 'AUTHORITY', "\"${authority}\""
        vectorDrawables.useSupportLibrary true
    }

    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("${rootDir}/keystore/debug.jks")
            storePassword 'android'
        }
        release {
            keyAlias ''
            keyPassword ''
            storeFile file("${rootDir}/keystore/release.jks")
            storePassword ''
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            manifestPlaceholders = [allowBackup: false, fullBackupContent: false,
                                    authority  : authority]
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [allowBackup: true, fullBackupContent: true,
                                    authority  : authority]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            File alignedOutputFile = output.outputFile
            if (alignedOutputFile != null && alignedOutputFile.name.endsWith('.apk')) {
                String buildName;

                def buildType = variant.buildType.name
                if ("debug".equals(buildType)) {
                    // We can't add date to debug builds because of an issue of Android Studio.
                    // Sometimes IDE pushes old build to device instead of the latest one.
                    // That's why the name of debug builds should be the same.
                    buildName = "${appName}_" + "${variant.buildType.name}"
                } else {
                    def df = new SimpleDateFormat("dd.MM.yy")
                    df.setTimeZone(TimeZone.getTimeZone("UTC"))

                    buildName = "${appName}_" +
                            "${variant.mergedFlavor.versionName}_" +
                            "${df.format(new Date())}_" +
                            "${variant.buildType.name}"
                }

                if (variant.buildType.zipAlignEnabled) {
                    output.outputFile = new File(alignedOutputFile.parent, buildName + '.apk')
                }
            }
        }
    }
}

dependencies {
    def supportLibVersion = "25.3.1"
    def utilsVersion = "1.1.2"

    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:percent:$supportLibVersion"
    compile "com.android.support:gridlayout-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"

    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile "com.evgeniysharafan.utils:utils:$utilsVersion"
    compile "com.evgeniysharafan.utils:recycler:$utilsVersion"
    compile "com.evgeniysharafan.utils:picasso:$utilsVersion"

    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'com.getbase:floatingactionbutton:1.10.1'

    compile 'org.jetbrains:annotations-java5:15.0'
}
